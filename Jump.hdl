// File name: Jump.hdl

/**
 * The jump unit determines whether the Program Counter should jump.
 * A jump is performed if any of the j1 j2 j3 bits of the c-instruction 
 * are true, AND the matching condition is met:
 *   j1: jump if ALU output is < 0
 *   j2: jump if ALU output   == 0
 *   j3: jump if ALU output is > 0
 * If multiple of these bits are set true, then jump if any of 
 * those conditions are met,
 * e.g. if j1 and j2 are both set true, jump if < 0 or == 0 
 * The ALU output is determined through the zr and ng wires:
 *   zr: is true when the ALU's output   == 0, false otherwise
 *   ng: is true when the ALU's output is < 0, false otherwise
 * If both zr and ng are false, then the ALU's output is > 0
 */

CHIP Jump {

    IN  j1,             // c-instruction j1
        j2,             // c-instruction j2
        j3,             // c-instruction j3
        zr,             // zr pin from ALU
        ng;             // ng pin from ALU

    OUT jump;           // true if a jump should be performed, false otherwise 

PARTS:
    // Condition 1: Jump if ALU output is < 0 and j1 is true
    And(a=j1, b=ng, out=jumpJLT);

    // Condition 2: Jump if ALU output == 0 and j2 is true
    And(a=j2, b=zr, out=jumpJEQ);

    // Condition 3: Jump if ALU output is > 0 (not zero and not negative) and j3 is true
    Not(in=zr, out=notZr);     // ALU output is not zero
    Not(in=ng, out=notNg);     // ALU output is not negative
    And(a=notZr, b=notNg, out=pos); // ALU output is > 0 (positive)
    And(a=j3, b=pos, out=jumpJGT);

    // Combine all jump conditions
    Or(a=jumpJLT, b=jumpJEQ, out=or1);
    Or(a=or1, b=jumpJGT, out=jump);
}